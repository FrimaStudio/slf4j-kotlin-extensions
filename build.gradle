plugins {
    id "net.researchgate.release" version "2.8.0"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "org.jetbrains.dokka" version "0.9.17"
}

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.slf4j:slf4j-api:$slf4j_version"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

    testCompile "org.jetbrains.kotlin:kotlin-reflect"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    testCompile ('com.nhaarman:mockito-kotlin:1.4.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    testCompile "org.slf4j:slf4j-simple:$slf4j_version"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    // Make sure this is usable on Android by forcing to use Java 1.6 bytecode
    kotlinOptions.jvmTarget = "1.6"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
    required = { gradle.taskGraph.hasTask("uploadArchives") }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    processConfigurations = ["compile", "compileOnly"]

    externalDocumentationLink {
        url = new URL("https://www.slf4j.org/api/")
    }

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/FrimaStudio/slf4j-kotlin-extensions/tree/master/src/main/kotlin"
        suffix = "#L"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'slf4j-kotlin-extensions'
                packaging 'jar'
                description 'Utilities to make working with slf4j Loggers from Kotlin more enjoyable.'
                url 'https://github.com/FrimaStudio/slf4j-kotlin-extensions'

                scm {
                    connection 'scm:git:https://github.com/FrimaStudio/slf4j-kotlin-extensions'
                    developerConnection 'scm:git:git@github.com:FrimaStudio/slf4j-kotlin-extensions.git'
                    url 'https://github.com/FrimaStudio/slf4j-kotlin-extensions'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'fdeschenes'
                        name 'Frederick Deschenes'
                        email 'frederick.deschenes@frimastudio.com'
                    }
                }
            }
        }
    }
}

release {
    buildTasks += ['uploadArchives']
}
